import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
    id "org.openapi.generator" version "7.5.0"
}

group = 'ru.wolfofbad'
version = '0.0.1'

java {
    sourceCompatibility = '21'
}

def kotlinReflectVersion = '1.9.23'
def springdocVersion = '2.5.0'
def springBootVersion = '3.2.5'
def kotestVersion = '5.8.1'
def kotestWiremockVersion = '3.0.1'
def jsoupVersion = '1.17.2'
def googleApiVersion = '1.35.2'
def googleOauthVersion = '1.35.0'
def googleSheetsVersion = 'v4-rev612-1.25.0'

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinReflectVersion"

    // Spring
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-log4j2:$springBootVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"

    // Google Api
    implementation "com.google.api-client:google-api-client:$googleApiVersion"
    implementation "com.google.oauth-client:google-oauth-client-jetty:$googleOauthVersion"
    implementation "com.google.apis:google-api-services-sheets:$googleSheetsVersion"

    // HTML parsing
    implementation "org.jsoup:jsoup:$jsoupVersion"

    // Test
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"
    testImplementation "io.kotest.extensions:kotest-extensions-wiremock:$kotestWiremockVersion"
}

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

def generatedPackage = "ru.wolfofbad.elibraryapi.generated"
openApiGenerate {
    inputSpec.set(project.file("./src/main/resources/openapi/openapi.yml").path)
    packageName.set("$generatedPackage")
    modelPackage.set("$generatedPackage..model")
    apiPackage.set("$generatedPackage..api")
    generatorName.set("kotlin-spring")
    configOptions.set(
            [
                    "dateLibrary"  : "java8",
                    "interfaceOnly": "true",
                    "useTags"      : "true"
            ]
    )
}

compileKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
        freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
    }
}

compileTestKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
        freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
    }
}

test {
    useJUnitPlatform()
}
